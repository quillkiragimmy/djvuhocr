#!/usr/bin/env perl
use strict;
use warnings;

use Getopt::Long 'GetOptions';
use Pod::Usage;

$| = 1;
my $imgtmp = '/tmp/.djvuimgtmp';
my $ocrtmp = '/tmp/.ocrlist';
my $djvu_script = '/tmp/.djvu_script';

GetOptions (
	'language|l=s' => \my $lang,
	'help|h' => \my $help,
	'debug|d' => \my $debug,
	'job|j=i' => \my $thread_num,
	'start_page|s=i' => \my $page_start,
	'last_page|e=i' => \my $page_last_force,
) or pod2usage ( -verbose => 1 );

$lang ||= 'eng';
my $tess_err = $debug? '' : '2>/dev/null';
$thread_num ||= 1;
$page_start ||= 1;

if ( $#ARGV < 0 or $help ) {
	pod2usage ( -verbose => 2 );
	exit 0 ;
#	$djvu_src = '~/ebooks/music/Mark Levine - Jazz Theory.djvu';
}

my $page_last;
my @djvu_list = @ARGV if $ARGV[0];

foreach my $djvu_src ( @djvu_list ) {
	print "processing '$djvu_src' with $thread_num threads.\n";

	$page_last = $page_last_force || `djvused "$djvu_src" -e 'n'`;
	chomp $page_last;
	my $page_num = $page_last - $page_start + 1;
	print ( $page_num, " pages: $page_start - $page_last\n" );

	my $thread_count = 0;
	my $page_count = 0;
	foreach my $page ( $page_start .. $page_last ) {
		if ( $thread_count >= $thread_num ) {
			wait;
			$thread_count--;
			$page_count++;
			print "progress: $page_count / $page_num.\n";
		}
		ocr ( $djvu_src, $page );
		$thread_count++;

	}

	while ( wait() != -1 ){
		$page_count++;
		print "progress: $page_count / $page_num.\n";
	}

#	replacing text layer.
	open ( my $DJVUSCRIPT, ">", $djvu_script ) || die "can't open '$djvu_script': $!";
	foreach ( $page_start .. $page_last ) {
		print $DJVUSCRIPT "select $_; remove-txt; set-txt $ocrtmp$_;\n";
	}
	close $DJVUSCRIPT;
	print "embedding text layer on pages.\n";
	`djvused "$djvu_src" -f "$djvu_script" -s`;

#	cleaning up for ocrtmos & djvu script.
	my @junks = ( $page_start .. $page_last );
	map { s/^/$ocrtmp/ } @junks;
	unlink ( @junks, $djvu_script );

}


sub ocr {# page.
	my $pid = fork();
	return if ( $pid );

	my ( $djvu_source, $page ) = @_;
	`ddjvu -format=tiff -page="$page" "$djvu_source" $imgtmp$page`;

	open ( my $OCR , ">", "$ocrtmp$page" ) || die "can't open '$ocrtmp': $!";
	my $hocr = `tesseract $imgtmp$page stdout -l $lang hocr $tess_err`;
	my $djvutxt = hocr_to_djvutxt ( $hocr );

	print $OCR "$djvutxt";
	close $OCR;
	unlink "$imgtmp$page";

	exit;

}


sub hocr_to_djvutxt {
	my $hocr = shift;
	$hocr =~ s/\n//g;
	$hocr =~ s/.*<body>(.*)<\/body>.*/$1/;

	$hocr =~ s/<div class='ocr_page'.*?bbox (\d+\s\d+\s\d+\s\d+).*?>/(page $1\n/g;
	$hocr =~ s/<div class='ocr_carea'.*?bbox (\d+\s\d+\s\d+\s\d+).*?>/(column $1\n/g;
	$hocr =~ s/<p class='ocr_par'.*?bbox (\d+\s\d+\s\d+\s\d+).*?>/(para $1\n/g;
	$hocr =~ s/<span class='ocr_line'.*?bbox (\d+\s\d+\s\d+\s\d+).*?>/(line $1\n/g;
	$hocr =~ s/<span class='ocrx_word'.*?bbox (\d+\s\d+\s\d+\s\d+).*?>(.*?)<\/span>/(word $1 "$2")\n/g;
	$hocr =~ s/<\/(span|p|div)>/)\n/g;
	$hocr =~ s/<.*?>//g;
	$hocr =~ s/\\/\\\\/g;
	$hocr =~ s/&quot;/‚Äù/g;
	$hocr =~ s/&#39;/'/g;

	# dirty fix of the 'upsidedown' problem. flip y.
	my @arr = split "\n", $hocr;
	my $page_height = 0;

	foreach ( @arr ) {
		if ( /^.+page\s(\d+)\s(\d+)\s(\d+)\s(\d+)$/ ) {
			$page_height = $4;
		}
		elsif ( /^(.+?)\s(\d+)\s(\d+)\s(\d+)\s(\d+)(.*)$/ ) {
			$_ = join ( ' ', ($1, $2, $page_height-$3, $4 , $page_height-$5, $6) );
		}
		else {
		}
	}
	return join ( "\n", @arr );
}

__END__
=head1 NAME

djvuhocr - apply tesseract OCR result to Djvu files.


=head1 SYNOPSIS

djvuhocr [-hd] [-j <thread_number>] [-l <language_code>] [-s <start_page>] [-e <last_page>] djvufile ...


=head1 OPTIONS

=over 16

=item B<-h, --help>

print this help.

=item B<-d, --debug>

tesseract debug masseges.

=item B<-l, --language = I<laguage>>

default is 'eng'.
For available language codes, please check "man tesseract".

=item B<-j, --job = I<thread_number>>

default is '1'.

=back

=head1 AUTHOR

Any bug report will be welcome at 'quillkiragimmy@gmail.com'.

=cut

